
// note: you may have to mess with this gradle file a bit, i have a local python version installed
//on my computer this is using, you can also specify a specific python path.


plugins {
    alias(libs.plugins.android.application)
    id("com.chaquo.python")
}

android {
    namespace 'com.ece420_parametric_eq'
    compileSdk 35

    defaultConfig {
        applicationId "com.ece420_parametric_eq"
        minSdk 33
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

chaquopy {
    defaultConfig {
        pip {
            // native runtimes
            install "chaquopy-openblas"
            install "chaquopy-libgfortran"
            // JPEG support for Pillow
            install "chaquopy-libjpeg"
            // PNG support for Pillow (if you ever load PNGs)
            install "chaquopy-libpng"

            // core libraries
            install "numpy==1.19.5"
            install "scipy==1.4.1"
            install "pandas==1.3.2"

            // matplotlib + its Python deps
            install "packaging"
            install "pyparsing"
            install "cycler"
            install "python-dateutil"
            install "kiwisolver"
            install "matplotlib==3.6.0"

            install "Pillow==9.2.0"
            install "soundfile==0.10.3.post1"
            install "tqdm==4.67.1"
            install "PyYAML==6.0.2"

            // finally AutoEQ
            install "autoeq==4.1.2"
        }
    }
}

dependencies {
    implementation 'com.jjoe64:graphview:4.2.1'
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation libs.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}
